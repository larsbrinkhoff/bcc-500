MSG    MACRO D,G,2; LDA =G(1); LDB =-1; LDX =1; BRS 34; BRU G(2)
G(1)   ASC "D(1)"
G(2)   EQU *; FRGT G(1),G(2); ENDM

R1     MSG ($SAVE AREA SY/); LDB =10; LDX =0; BRS 38; STA AREA
       CBA; SKE ='.'; BRU R1
       LDX AREA; LDA BASE,2; LRSH 14; STA TRACK
       CLA; LSH 7; STA BAND; STA MINB; ADD =4; STA MAXB
       CLA; LSH 5; STA SECT
       CLA; LSH 2; STA UNIT
       MSG ($TO FILE /); CLEAR; BRS 16; BRU R1; STA FILE
R2     TCO =155B; TCO =152B; LDA =24; BRM XFER; BRM INC
       LDA =8; BRM XFER; BRS 17; BRS 10

XFER   ZRO; STA ENDCNT; LDA =1; STA COUNT
X1     BRM READ; LDA NODE+21B; SKA =3777B; ZRO *
       LDA =4000B; LDX =RBUF; BIO FILE; ZRO *
       BRM INC; LDA COUNT; SKG ENDCNT; BRU X1; BRR XFER

INC    ZRO; MIN COUNT; MIN SECT; LDA SECT; SKG MAXS; BRR INC
       LDA MINS; STA SECT; MIN BAND; LDA BAND; SKG MAXB; BRR INC
       LDA MINB; STA BAND; MIN TRACK; BRR INC

READ   ZRO; CLA; STA NODE; STA NODE+1
       LDA TSU; CLB; LSH 8; MRG TRACK; LSH 6; MRG BAND; LSH 4
       MRG SECT; LSH 2; MRG UNIT; STA NODE+2
       CLA; STA NODE+3
       LDA WDCT; CLB; LSH 18; MRG =205B; STA NODE+4
       LDA INST; CLB; LSH 18; MRG =777777B; STA NODE+5
       LDA =NODE; LDB =18; LDX CMA; BRS 137; BRR READ

AREA   BSS 1
BAND   BSS 1
COUNT  BSS 1
ENDCNT BSS 1
FILE   BSS 1
NODE   BSS 20
SECT   BSS 1
TRACK  BSS 1

BASE   DATA 40040000B          SY0
       DATA 42340000B          SY1
       DATA 44640000B          SY2
       DATA 47140000B          SY3
       DATA 51440000B          SY4
       DATA 53740000B          SY5
       DATA 40041400B          SY6
       DATA 42341400B          SY7
       DATA 44641400B          SY8
       DATA 47141400B          SY9
       DATA 51441400B          SY10
       DATA 53741400B          SY11
CMA    DATA RBUF
INST   DATA 0
MAXB   DATA 4
MINB   DATA 0
MAXS   DATA 4
MINS   DATA 0
TSU    DATA 2
UNIT   DATA 0
WDCT   DATA 11

RBUF   EQU     34000B
START  EQU     R1

       END