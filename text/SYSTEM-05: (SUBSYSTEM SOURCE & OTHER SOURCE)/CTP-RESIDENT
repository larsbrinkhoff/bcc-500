*      BSS     20002B          THIS MAKES THE LISTING ADDRESSES ABSOLUTE
*      LIST    ALL,GO
* CTPRES IDENT


* PROGRAM TO INITIALIZE CHIO LOCAL CORE AND
*    CERTAIN CENTRAL MEMORY TABLES TO ACCOMMODATE
*    LOCAL CORE BUFFERING.

* PROGRAM GETS ITS PARAMETERS FROM THE CHIO SCRATCHPADS.

* SCRATCHPAD DECLARATIONS:

BUF0   EQU     53              ORIGIN OF BUFFER STORAGE AREA
DVTB   EQU     10              ORIGIN OF DVTB TABLE
FREEL  EQU     41              BEGINNING OF FREE LIST
LBTBA  EQU     28              ORIGIN OF LINE BIT TABLE
MTAA   EQU     1               ORIGIN OF MAIN CHIO LINE TABLE
MTCA   EQU     2               ORIGIN OF CHARACTER POINTER LINE TABLE

* OTHER PARAMETERS:

GLITCH EQU     10              WIDTH OF GLITCH FILTER

* MICROCODE-RELATED DECLARATIONS:

UCALL  OPD     43B5,2          MICROCODE CALL INSTRUCTION
GLE    EQU     514B            MICROCODE ADDRESS FOR GLE ROUTINE
INL2   EQU     701B            MICROCODE ADDRESS FOR INL2 ROUTINE
WAKEUP EQU     1334B           MICROCODE ADDRESS FOR WAKEUP ROUTINE

*THE FIRST PROGRAM INITIALIZES THE CHIO.

INIT   LDX     =-100B
I1     LDA     /ISTATE+100B    LOAD CHIO WITH CORRECT INITIAL STATE
       XCA     /100B
       BRX     I1
       LDA     MTAPTR
       STA     POINTR
       LDX     =-3000B
I2     LDA     /TABLES+3000B   LOAD CORRECT CHIO TABLES
       STA     ←POINTR
       BRX     I2

* PROGRAM TO INITIALIZE CHIO BUFFER SPACE

BUFFER CCA     FREEL           FREEL CONTAINS NO. BUFFERS * 10B
       RSH     3               GETS JUST NO. OF BUFFERS
       STA     ←P41B           UPDATES C.M. LOCATION 41B
       LSH     3               GETS BACK NO. BUFFERS * 10B
       ADD     =10B            (NO. BUFFERS + 1)*10B
       STA     POINTR          TEMPORARILY
       CNA
       STA     XVAL            INITIAL INDEX REG. VALUE IN LOOP L2
       CNA
       SUB     =20B            (NO. BUFFERS - 1)*10B
       STA     ETC             END TEST CONSTANT
       CCA     BUF0            BUF0 CONTAINS START OF BUFFER AREA
       LDB     =4B7            CENTRAL MEMORY BIT
       SKA     =2B6
       CLB                     IF LOCAL
       STB     PTR1            ANOTHER POINTER TEMPORARILY
       ETR     =377777B        GET ADDRESS PROPER
       ADM     PTR1            MAKE A BASIC POINTER
       LDA     PTR1
       ADD     POINTR          UPDATE POINTER
       MRG     =2B7            INCLUDE INDEXING BIT FOR LOOP
       STA     POINTR
       LDA     PTR1            UPDATE FOR LOOP L3
       ADD     =10B
       STA     PTR1
       LDX     XVAL
       CLA
L2     STA     ←POINTR         CLEAR OUT BUFFER AREA
       BRX     L2
       STA     LINK            INITIALIZE FOR LOOP L3
L3     LDA     LINK
       STA     ←PTR1           CREATE LINKS IN BUFFER AREA
       LDA     =10B
       ADM     PTR1
       LDA     LINK
       ADD     =10B
       STA     LINK
       SKG     ETC             END TEST
       BRU     L3              NOT DONE:  LOOP


* PROGRAM TO INTERFACE CHIO LOCAL TELETYPE AND PAPER TAPE READER
*    WITH SYSTEM LOGICAL LINES.

LOCTTY CCA     MTCA            MTCA CONTAINS ORIGIN OF CHIO CHR. PTRS.
       STA     POINTR          TEMPORARILY
       CCA     LBTBA           LBTBA CONTAINS ORIGIN OF LOCAL DEVICE TABLE
       LDB     =4B7            CENTRAL MEMORY BIT
       SKA     =2B6            TEST CENTRAL/LOCAL
       CLB                     IF LOCAL
       ETR     =377777B        GET ADDRESS PROPER
       STA     PTR1
       CBA
       ADM     PTR1            BASE OF LOCAL DEVICE TABLE
       CLB
       LDA     ←P7300B         TERMINAL NO. FOR LOCAL TELETYPE CONNECTION
       LSH     1
       STA     INLIN           LOGICAL INPUT LINE NUMBER
       ADD     =1
       STA     OUTLIN          LOGICAL OUTPUT LINE NUMBER
       LDA     ←P7302B         TERMINAL NO. FOR P.T. CONTROL LINE
       LSH     1
       STA     CLIN            CONTROL LOGICAL INPUT NUMBER
       ADD     =1
       STA     CLON            CONTROL LOGICAL OUTPUT NUMBER
       LDA     ←P7303B         TERMINAL NO. FOR P.T. DATA LINE
       LSH     1
       STA     PTDLN           PAPER TAPE DATA LINE NUMBER
       ADD     ←P7303B
       LSH     1
       ADD     =2              THIRD WORD IN ENTRY
       ADD     POINTR
       STA     PTILCC          P.T. INPUT LINE CHARACTER COUNT
       LDB     =4B7            CORRECTS PTILCC TO PROPER MEMORY
       SKA     =2B6
       CLB
       CBA
       ADM     PTILCC
       CCA     MTAA            MTAA CONTAINS ORIGIN OF MAIN LINE TABLE
       LDB     =4B7            CENTRAL MEMORY BIT
       SKA     =2B6            TEST CENTRAL/LOCAL
       CLB
       ETR     =377777B
       STA     PTR2
       CBA
       ADD     PTR2
       STA     PTR2
       ADD     =2B7            ADD INDEX BIT TO MEMORY BIT
       STA     PMTA            FOR USE WITH INDEXING LATER.
       CCA     DVTB            DVTB ASSOCIATES LOG. LINE W/ B.S. LINE
       LDB     =4B7            CENTRAL MEMORY BIT
       SKA     =2B6            TEST CENTRAL/LOCAL
       CLB
       ETR     =377777B
       STA     PLDVT
       CBA
       ADD     =2B7            ADD INDEX BIT TO MEMORY BIT
       ADM     PLDVT
       LDA     ←P7302B         GET P.T. CONTROL LINE NO.
       BRM     LPARAM          COMPUTE LINE PARAMETERS
       STA     CLOCW           CONTROL LOGICAL OUTPUT CONTROL WORD
       STB     CLOI            CONTROL LINE OUTPUT INDICATOR
       LDA     ←P7300B         GET LOCAL TTY LINE NO.
       BRM     LPARAM          COMPUTE LINE PARAMETERS
       STA     POINTR
       STB     MASK
       LDA     =PTINIT-1
       STA     LTTY            INITIALIZE PAPER TAPE COROUTINE
       CLA                     NOW START UP CHIO MICROCODE...
       XMDA                    ...WHILE CONTINUING FORWARD FROM HERE

* MAIN LOOP LOOKS AT LOCAL TELETYPE AND PAPER TAPE READER, SERVICING
*    INPUT AND OUTPUT AT THE PROPER TIMES.

L4     BRM     PTAPE           SERVICE PAPER TAPE READER
       LDA     =4B7            READ LOCAL TTY STATUS
       PIN
       SKB     =2              SKIP IF NO CHARACTER HAS BEEN RECEIVED
       BRM     INCHR           GO GET INCOMING CHARACTER
L4A    LDA     ←POINTR         CHECK CHIO TABLES FOR PENDING OUTPUT
       SKA     MASK            SKIP IF NO OUTPUT TO DO
       BRU     *+2
       BRU     L4              BEGIN MAIN LOOP
       LDA     =4B7            READ NEW LOCAL TTY STATUS
       PIN
       SKB     =4              SKIP IF TRANSMITTER BUSY
       BRM     OUTCHR          GO SEE IF THERE IS A CHARACTER FOR OUTPUT
       BRU     L4              END MAIN LOOP

* SUBROUTINE TO READ NEW CHARACTER FROM LOCAL TELETYPE RECEIVER
*    AND PLACE IN A CHIO BUFFER.

INCHR  ZRO
       LDA     =4B7+2          GET CHR FROM LOCAL TTY BOARD AND CLR STATUS
       PIN
       CBA
       SKA     =2B7            TEST FOR RECEIVER OVERRUN BIT
       MIN     ←P7301B         USE C.M. LOCATION 7301B FOR ERROR COUNTER
       ETR     =377B
       CAB
       LDA     INLIN           LOGICAL INPUT LINE NUMBER
       BRM     PUT             PUT CHARACTER INTO CHIO BUFFER
       BRR     INCHR           RETURN

* SUBROUTINE TO CHECK CHIO OUTPUT LOGICAL LINE AND OUTPUT
*    THROUGH LOCAL TELETYPE BOARD.  RETURNS TO MAIN
*    LOOP IF THERE IS NOTHING TO OUTPUT.

OUTCHR ZRO
       LDB     OUTLIN
       BRM     GET             GET CHARACTER FROM CHIO: SKIP ON SUCCESS
       BRR     OUTCHR          IF THERE IS REALLY NO CHARACTER
       LDA     =4B7+4          SEND CHR ON LOCAL TTY LINE
       POT
       BRR     OUTCHR          RETURN

* SUBROUTINE TO COMPUTE LINE PARAMETERS

LPARAM ZRO
       STA     LT1             TEMP1 HOLDS CPU LINE NO.
       CLB
       LSH     1               2*L
       ADD     =1              2*L+1
       STA     LT2             TO MULTIPLY BY 3
       ADD     LT2
       ADD     LT2             3*(2*L+1)
       ADD     POINTR          LTH OUTPUT ENTRY IN MTC FOR HASHING
       ETR     =16B            COMPUTE HASHING FOR LBT TABLE
       SUB     =1
       SKG     =-1
       LDA     =1
       ADD     PTR1            LBTBA
       STA     LT2             TEMPORARILY
       LDA     LT1             TAKE CARE OF DEVICE TYPE
       LSH     2               4*L
       ADD     PTR2            MTAA
       STA     LT1             ADDR OF LTH ENTRY IN MTA
       LDA     ←LT1            FETCH 0TH WORD OF ENTRY
       ETR     =77B            EXTRACT DEVICE TYPE
       CAX
       LDA     =3
       ADM     LT1
       LDA     ←LT1            FETCH 3RD WORD OF ENTRY
       LRSH    16              GET LOCAL DEVICE NO. FOR GIVEN TYPE
       STA     LT1             HOLD LOCAL DEVICE NO.
       LDA     /DTYPE          OFFSET INTO LBTB BY DEVICE TYPE
       ADM     LT2
       LDA     =23             COMPUTE MASK BIT FOR LOCAL DEVICE NO.
       SUB     LT1
       CAX
       LDA     =1
       CLB
       LSH     /0
       CAB                     PUT MASK IN B
       LDA     LT2             PUT OUTPUT CONTROL WORD IN A
       BRR     LPARAM          RETURN

* COROUTINE LINKAGES: LOCAL TTY BOARD HANDLER AND PAPER TAPE ROUTINES

PTAPE  BSS     1
       BRR     LTTY
LTTY   BSS     1               REQUIRES INITIALIZATION TO PTINIT-1
       BRR     PTAPE

* COROUTINE TO HANDLE PAPER TAPE READER
*    THIS CODE IS MADE TO BE A COROUTINE TO FACILITATE TIMING
*    CONSIDERATIONS RELATIVE TO CODE HANDLING THE 9600-BIT
*    ASYNCHRONOUS LINE.  IT CALLS LTTY EVERY 15 OR 20 INSTRUCTIONS
*    IN ORDER TO FACILITATE FULL-SPEED OPERATION OF THE LINE.

* THE FOLLOWING CODE SERVICES THE PSEUDO-LINE WHICH CONTROLS THE
*    PAPER-TAPE READING CODE.

PTINIT CLA
       STA     STATE
       STA     LSTATE
       STA     STRING
       LDA     INOP
       STA     RDINST

PTLOOP CLA                     CHECK C.M. WINDOW
       SKE     ←P7304B         SEE IF ADDRESS IS NOT ZERO
       BRM     FETCH           FETCH CONTENTS OF INDICATED WORD
       SKE     ←P7306B         SEE IF ADDRESS IS NOT ZERO
       BRM     STORE           STORE CONTENTS INTO INDICATED WORD
       LDA     ←CLOCW          CONTROL LINE OUTPUT CONTROL WORD
       SKA     CLOI            CONTROL LINE OUTPUT INDICATOR
       BRU     PTL0            IF THERE IS A COMMAND CHR
       BRM     LTTY            SERVICE LOCAL TTY BOARD

* THIS IS A HORRIBLE KLUDGE TO GET AROUND A BAD STATE WHICH HAPPENS
* SOMETIMES IN THE CHIO.  PROBABLY BECAUSE ONE CAN'T REALLY CHANGE
* TERMINAL DATA RATES ON THE FLY, IT HAPPENS INFREQUENTLY THAT THE
* NCIP WORD FOR TYPE 1 (300 BPS) TERMINALS GET SET TO NONSENSE.
* THIS CODE MONITORS THE WORD, LOOKING FOR CHANGES.  WHEN IT SEES ONE,
* IT RESETS A TIMER.  IF THE CONDITION EVER HAPPENS, THE TIMER WILL
* GO OFF, AND AN OVERRIDE OF THE CONDITION WILL BE ATTEMPTED.

       LDA     ←LBTB1          INSPECT NCIP1
       SKE     LAST            SEE IF SAME AS LAST INSPECTION
       BRU     KL3             NO...RESET TIMER AND CONTINUE
       MIN     TIMER           YES...INCREMENT TIMER 
       LDA     TIMER
       SKG     TROUBLE         CHECK NEW VALUE
       BRU     KL4             NO PROBLEM YET

* NOW WE HAVE FOUND A PROBLEM.  WE TRY TO FIX IT.

       CLA
       STA     NCIP1
       STA     PLNO
KL1    BRM     LTTY
       LDA     PLNO            LOOK UP RELATED CPU NO FOR EACH BS LINE
       CLB
       LSH     1               PLNO * 2
       ADD     =1              COMPUTE OFFSET IN TABLE
       CAX
       LDA     ←PLDVT
       ETR     =7777B          NOW LOOK UP DEVICE TYPE IN MAIN TABLE
       LSH     1               PTNO * 4
       CAX
       LDA     ←PMTA
       ETR     =77B
       SKE     =1              SEE IF DEVICE TYPE IS 1
       BRU     KL2             NO...TRY NEXT LINE
       LDA     =4B7            YES...PUT IN NCIP1
       LDX     PLNO
       LRSH    /0
       ADM     NCIP1
KL2    MIN     PLNO            INCREMENT LINE & TRY EACH UP TO 23
       LDA     PLNO
       SKG     =23
       BRU     KL1
       LDA     NCIP1           THIS IS WHAT WE THINK IT SHOULD BE.
       STA     ←LBTB1

KL3    STA     LAST            SAVE FOR FUTURE COMPARISONS
       CLA
       STA     TIMER           RESET TIMER
KL4    BRM     LTTY

* HERE WE CHECK TO SEE IF AN RS-232 LINE'S STATUS HAS CHANGED.

       LDA     =102B           READ "DSR" STATUS FOR RS-232 LINES
       PIN
       CBA
       EOR     LSTATE          COMPARE
       SKE     =0
       BRU     ST1             THERE HAS BEEN A CHANGE: CHECK IT OUT.
       BRM     LTTY
       LDX     =-24            CLEAR GLITCH FILTER COUNTS
       STA     /GCOUNT+24
       BRX     *-1
       BRM     LTTY
ST0    EXU     RDINST          PERHAPS SERVICE P.T. READER
       BRU     PTLOOP
       BRM     TDTP            TERMINATE PAPER TAPE HANDLER
       BRM     LTTY
       BRM     NG              RETURN ERROR MESSAGE
       BRM     LTTY
       CLA                     SET TO INITIAL STATE
       BRU     PTL3

PTL0   LDB     CLON            GET CHR FROM P.T. CONTROL LINE
       BRM     GET
       BRU     PTLOOP          IF FOR SOME REASON THERE IS NONE
       STB     PTCHR           SAVE
       BRM     LTTY            SERVICE LOCAL TTY BOARD
       LDA     PTCHR           EXAMINE NEW CHARACTER
       SKE     =15B            TEST FOR CR
       BRU     PT1             IF NOT, ADD TO STRING
       LDX     =-3
       LDA     STRING          SEARCH FOR COMMAND
PTL1   SKE     /INPUT+3
       BRU     PT2             CONTINUE SEARCH LOOP
PTL2   CXA                     COMPUTE COMMAND INDEX
       LSH     12
       RSH     12
       CNA
       MRG     =2B7            INDEX BIT
       STA     CMND
       BRM     LTTY            SERVICE LOCAL TTY BOARD

* NOW EXECUTE THE COMMAND.  GO THROUGH THREE TABLES.  THE 1ST, ACTION,
*    ESTABLISHES/DESTROYS THE PAPER TAPE DATA TRANSFER ROUTINE.
*    THE 2ND PERFORMS ACCOMPANYING CONTROL LINE OUTPUT.  THE 3RD
*    CHANGES TO THE NEXT STATE.

       LDX     STATE
       EAX     ←/ACTION        ESTABLISH/DESTROY DTP
       EXU     ←CMND
       LDX     STATE
       EAX     ←/OUTPUT        DO ACCOMPANYING OUTPUT
       EXU     ←CMND
       BRM     LTTY            SERVICE LOCAL TTY BOARD
       LDX     STATE
       EAX     ←/NEXT          CHANGE TO NEXT STATE
       EXU     ←CMND
PTL3   STA     STATE
       CLA                     CLEAR COMMAND
       STA     STRING
       BRU     PTLOOP          BACK TO START OF MAIN LOOP

PT1    EOR     =140B           CONVERT TO 940 ASCII
       RSH     6
       LDA     STRING          ADD NEW CHARACTER TO STRING
       LSH     6
       STA     STRING
       BRU     PTLOOP

PT2    BRX     PTL1
       BRU     PTL2

ST1    BRM     HANDLE          TAKE CARE OF A SINGLE CHANGE
       SKE     =0
       BRU     ST1             IF ANOTHER LINE CHANGED ON THE SAME SAMPLE
       BRU     ST0


* SUBROUTINE TO HANDLE AN RS-232 LINE STATUS CHANGE

HANDLE ZRO
       STA     SAMPLE
       CLB
       STB     PLNO            PHYSICAL LINE NUMBER
H0     BRM     LTTY
       CLB
       LDA     SAMPLE
       SKA     =4B7            TEST SIGN BIT
       BRU     H1              IF SET
       LSH     1
       STA     SAMPLE
       MIN     PLNO
       LDA     PLNO
       SKG     =23
       BRU     H0
       CLA
       BRR     HANDLE

H1     EOR     =4B7            TURN OUT THE BIT
       LDX     PLNO
       LRSH    /0              RESTORE JUSTIFICATION OF SAMPLE
       STA     SAMPLE
       MIN     /GCOUNT
       LDA     /GCOUNT
       SKG     =GLITCH
       BRU     *+2             STILL SMALL ENOUGH TO BE CONSIDERED GLITCH
       BRU     H2
HOUT   BRM     LTTY
       LDA     SAMPLE          RETURN SAMPLE MINUS PROCESSED BIT
       BRR     HANDLE

H2     BRM     LTTY
       LDA     =4B7            POST THE CHANGE OF STATE FOR PLNO
       LDX     PLNO
       LRSH    /0
       STA     ADD             SAVE FOR LATER (MAYBE)
       EOR     LSTATE          INVERT THE CORRECT BIT
       STA     LSTATE
       LSH     /0
       STA     CHANGE          SAVE DIRECTION OF CHANGE
       CLA
       STA     /GCOUNT         RESET GLITCH FILTER FOR LINE
       BRM     LTTY
       LDA     PLNO            GET PRTI FOR GIVEN LINE
       BRM     GTPRTI
       STA     PRTI
       STX     LLNO            LOGICAL LINE NUMBER
       BRM     LTTY
       SKN     CHANGE          TEST DIRECTION OF CHANGE
       BRU     H3              IF STATE WENT FROM 1 TO 0
       BRU     HOUT            IF STATE WENT FROM 0 TO 1

H3     BRM     LTTY
       LDA     PRTI
       SKE     =1              CHECK TO MAKE SURE IT'S NOT THE LISTENER
       BRU     *+2             OK TO PROCEED WITH DETACHMENT
       BRU     H4              IT IS...DON'T WANT TO DETACH LISTENER
       CLB                     SET UP A POINTER TO PROPER PIW
       LSH     2               4*PRTI
       STA     PRTPTR          TEMP.
       LSH     1               8*PRTI
       ADD     PRTPTR          12*PRTI
       ADD     PRTBASE
       ADD     =3
       STA     PRTPTR
       BRM     LTTY
       LDA     =-1             TURN OFF CHIO FOR A BRIEF TIME
       XMDA
       STA     MODE            TEMP.
       PRO     1               GET CONTROL OF PRT
       LDA     =4B6            CARRIER OFF INTERRUPT
       ADM     ←PRTPTR
       UPR     1
       LDA     MODE
       XMDA
       LDA     PRTI            SEND CHIO INTERRUPT TO CHOSEN PROCESS
       LDX     =6
       UCALL   WAKEUP
       BRM     LTTY


* SET LINE RATE FOR DETACHED TERMINAL TO 300 BPS

H4     LDA     LLNO            SEE IF LINE IS SET TO TYPE 2 (110 BPS)
       CLB
       LSH     1               CPU LINE * 4
       CAX
       LDA     ←PMTA           GET MAIN TABLE ENTRY
       ETR     =77B
       SKE     =2              TEST FOR DEVICE TYPE
       BRU     HOUT            LEAVE IT AS IS
       STX     LLNO            USE AS TEMP. FOR OFFSET IN MTA
       LDA     ADD             SET UP MASKS FOR SPEED CHANGE
       EOR     =-1
       STA     REMOVE          MASK FOR REMOVING FROM GROUP
       BRM     LTTY
       LDA     =-1
       XMDA                    TURN OFF CHIO FOR A TIME
       STA     MODE
       LDX     =-14
H5     LDA     ←LBTB2          CHECK LOC. DEV. BIT TABLE FOR ACTIVITY
       SKA     ADD
       BRU     H6              BUSY NOW.  TRY AGAIN LATER
       BRX     H5
       LDA     ←LBTB2          CHECK NCIP WORD
       SKA     ADD
       BRU     *+2             LINE PASSIVE.  CHANGE IT.
       BRU     H6              SOMETHING WRONG.  TRY LATER.
       ETR     REMOVE          REMOVE FROM TYPE 2 CATEGORY
       STA     ←LBTB2
       LDA     ADD             ...AND PLACE IN TYPE 1
       ADM     ←LBTB1
       LDX     LLNO            NOW FIX UP MAIN TABLE
       LDA     =24040101B
       STA     ←PMTA
       LDA     MODE
       XMDA                    TURN CHIO BACK ON
       BRU     HOUT

H6     LDA     MODE            LET'S BACK OUT AND TRY AGAIN LATER
       XMDA
       LDA     ADD
       EOR     LSTATE
       STA     LSTATE
       BRU     HOUT

* ROUTINE TO GET PRTI AND LLNO, GIVEN PLNO

GTPRTI ZRO
       CLB                     PLNO IS IN A
       LSH     1               2*PLNO
       ADD     =1
       CAX                     COMPUTE OFFSET IN TABLE
       LDA     ←PLDVT          FETCH ENTRY FROM LOCAL DEVICE TABLE
       ETR     =7777B          EXTRACT LOGICAL LINE NUMBER
       STA     GTPLNO          TEMP.
       LSH     1               2*LLNO=4*LTNO
       ADD     =3
       CAX                     COMPUTE OFFSET IN TABLE
       LDA     ←PMTA           FETCH ENTRY FROM MAIN TABLE
       ETR     =17777B         EXTRACT PRTI
       LDX     GTPLNO
       BRR     GTPRTI

* ROUTINE TO FETCH CONTENTS OF LOCAL CORE LOCATION
*    AND STORE INTO LOCATION 7305B OF C.M., CLEARING 7304B

FETCH  ZRO
       XMA     ←P7304B         GETS ADDRESS & CLEARS WINDOW
       CAX                     WILL USE INDEXING FOR ADDRESSING
       LDA     /0              DO INDICATED FETCH
       STA     ←P7305B         PUT INTO C.M.
       BRM     LTTY            BECAUSE WE TOOK SO MUCH TIME
       CLA
       BRR     FETCH           RETURN

* ROUTINE TO STORE CONTENTS OF C.M. LOCATION 7307B INTO LOCAL CORE
*    AT LOCATION GIVEN IN 7306B

STORE  ZRO
       XMA     ←P7306B         GETS ADDRESS & CLEARS WINDOW
       CAX                     FOR ADDRESSING
       LDA     ←P7307B         GET NEW CONTENTS
       STA     /0              PUT INTO LOCAL CORE LOCATION
       BRM     LTTY            BECAUSE WE TOOK SO MUCH TIME
       BRR     STORE           RETURN

* COROUTINE LINKAGES: PAPER TAPE COMMAND ROUTINE TO HANDLER ROUTINE

PTR    BSS     1
       BRR     PTL
PTL    BSS     1               REQUIRES INITIALIZATION TO PTREAD-1
       BRR     PTR

* ROUTINE TO HANDLE PAPER TAPE READER.  THIS ROUTINE GETS ACTIVATED
*    OR DEACTIVATED BY THE ACTION TABLE.  IT IS A COROUTINE ALSO.

PTREAD LDA     ←PTILCC         PAPER TAPE INPUT LINE CHAR. COUNT
       ETR     =7777B
       SKG     MAXCNT          TEST FOR ALLOTTED BUFFER SPACE FULL
       BRU     PTRD1           NOT FULL: TURN ON READER
       BRM     PTL             SERVICE COMMAND LOOP & LOCAL TTY BOARD
       BRU     PTREAD
PTRD1  LDA     =4B7+1
       LDB     =1              TURN ON READER
       POT
PTRD2  LDA     =4B7+1
       PIN                     READ STATUS
       SKB     =4B7            SKIP IF P.T. CHAR NOT AVAILABLE
       BRU     PTRD3           YES.
       BRM     PTL             NO: SERVICE COMMAND LOOP & LOCAL TTY BOARD
       BRU     PTRD2
PTRD3  CBA                     GET CHARACTER
       EOR     =4B7            INVERT STATUS BIT
       SKA     =7B7            SKIP IF NO PROBLEM
       BRU     PTRD5           PROBLEM: QUIT READING & ABORT
       ETR     =377B           GET JUST CHARACTER ITSELF
       STA     PTRCHR          SAVE
       BRM     PTL             SERVICE COMMAND LOOP & LOCAL TTY BOARD
       LDA     ←PTILCC         TEST BUFFER AGAIN
       ETR     =7777B
       SKG     MAXCNT          IF FULL, SKIP AND STOP P.T. READER
       BRU     PTRD4           NOT FULL: KEEP GOING
       LDA     =4B7+1
       LDB     =2              STOP READER
       POT
       LDB     PTRCHR
       LDA     PTDLN
       BRM     PUT             PUT CHARACTER INTO CHIO BUFFER
       BRU     PTREAD          CONTINUE LOOP
PTRD4  LDB     PTRCHR
       LDA     PTDLN
       BRM     PUT             PUT CHR IN CHIO BUFFER
       BRU     PTRD2
PTRD5  LDA     =4B7+1          PAPER TAPE ABORT
       LDB     =2              STOP READER
       POT
       MIN     PTR             SET UP FAIL RETURN
       BRM     PTL             FAIL RETURN

* SUBROUTINE TO GET CHARACTER FROM CHIO BUFFER
*    TAKES LOGICAL OUTPUT LINE NO. IN B.
*    SKIP RETURNS CHARACTER IN B IF SUCCESSFUL

GET    ZRO
       LDA     =-1             TURN OFF CHIO
       XMDA
       STA     MODE            FOR LATER RESTORATION
       CBA                     LINE NO. TO A
       UCALL   GLE             SET UP UCODE POINTERS, ETC.
       UCALL   ←OUTL2          GET CHARACTER
       MIN     GET             IF SUCCESSFUL
       CAB                     PUT CHARACTER IN B
       LDA     MODE            RESTORE CHIO OPERATION
       XMDA
       BRR     GET             RETURN

* SUBROUTINE TO PUT CHARACTER INTO CHIO BUFFER
*    TAKES LOGICAL INPUT LINE NO. IN A & CHARACTER IN B.

PUT    ZRO
       STA     PUTT1           HOLD LINE NO. TEMPORARILY
       STB     PUTT2           SAVE CHARACTER
       LDA     =-1             TURN OFF CHIO IF ON
       XMDA
       STA     MODE            SAVE FOR RESTORATION
       LDA     PUTT1           GET BACK LINE NO.
       UCALL   GLE             SET UP POINTERS FOR UCODE INPUT ROUTINE
       LDA     PUTT2           GET CHARACTER INTO A
       UCALL   INL2            PUT CHARACTER INTO CHIO BUFFER
       LDA     MODE            RESTORE NORMAL CHIO ACTIVITY
       XMDA
       BRR     PUT             RETURN

* SUBROUTINE TO INITIALIZE PAPER TAPE HANDLER

IDTP   ZRO
       LDA     =PTREAD-1
       STA     PTL
       LDA     IBRM
       STA     RDINST
       BRR     IDTP

* SUBROUTINE TO TERMINATE PAPER TAPE HANDLER

TDTP   ZRO
       LDA     =4B7+1
       LDB     =2
       POT
       LDA     INOP
       STA     RDINST
       BRR     TDTP

* SUBROUTINES TO OUTPUT VARIOUS RESPONSES ON PAPER TAPE CONTROL LINE

BYE    ZRO
       LDA     BYEMSG
       BRM     RESPND
       BRR     BYE

NG     ZRO
       LDA     NGMSG
       BRM     RESPND
       BRR     NG

OFFOK  ZRO
       LDA     =4B7+1
       PIN
       SKB     =1B7            SKIP IF POWER IS ON
       BRU     OFF1
       LDA     OKMSG
       BRM     RESPND
       CLA
       STA     OFFFLG
       BRR     OFFOK
OFF1   LDA     OFFMSG
       BRM     RESPND
       LDA     =-1
       STA     OFFFLG
       BRR     OFFOK

* SUBROUTINE TO ACTUALLY DO THE RESPONSE OUTPUT

RESPND ZRO
       STA     RESOUT          TEMPORARY STORAGE
RES1   LDB     RESOUT          ENTER A LOOP WHICH ENDS WITH A ZERO CHAR.
       CLA
       LSH     8
       STB     RESOUT
       SKE     =0              STOP ON ZERO CHARACTER
       BRU     RES2
       LDB     =15B            CARRIAGE RETURN ENDS RESPONSE
       LDA     CLIN            CONTROL LINE INPUT NUMBER
       BRM     PUT             PUT CHARACTER IN CHIO BUFFER
       BRM     LTTY
       BRR     RESPND
RES2   EOR     =140B           CONVERT TO REAL ASCII
       CAB
       LDA     CLIN            CONTROL LINE INPUT NUMBER
       BRM     PUT             PUT CHARACTER IN CHIO BUFFER
       BRM     LTTY            SERVICE LOCAL TELETYPE BEFORE CONTINUING
       BRU     RES1            NEXT CHARACTER

* SUBROUTINE TO SET UP CONDITIONAL NEXT STATE (IN CONJUNCTION WITH OFFOK)

WHICH  ZRO
       CLA
       SKN     OFFFLG
       LDA     =1              NEXT STATE IS 1 IF READER IS ON
       BRR     WHICH

* CONSTANTS

DTYPE  DATA    0,0,16,32       OFFSETS IN LBTB FOR DEVICE TYPES 0 - 3
IBRM   BRM     PTR             INSTRUCTION TO BE PUT INTO RDINST
INOP   NOP     0               INSTRUCTION TO BE PUT INTO RDINST
INPUT  TEXT    'STOP'          TABLE OF PAPER TAPE CONTROL COMMANDS
       TEXT    '  GO'
       TEXT    'PTRD'
MAXCNT DATA    629             PERMITS NO MORE THAN 30 CHIO BUFFERS
BYEMSG ASC     "BYE"
MTAPTR DATA    6B7+5140B+3000B
LBTB1  DATA    4B7+6376B       PTR TO NCIP WORD FOR DEVICE TYPE 1
LBTB2  DATA    6B7+6416B       PTR TO NCIP WORD FOR DEVICE TYPE 2
NGMSG  ASC     "NG "
OFFMSG ASC     "OFF"
OKMSG  ASC     "OK "
OUTL2  DATA    4B7+4743B       MICROCODE ADDRESS FOR OUTL2 ROUTINE
P41B   DATA    4B7+41B         POINTS TO C.M. LOCATION 41B
P7300B DATA    4B7+7300B       TERM. NO. FOR LOCAL TTY CONNECTION
P7301B DATA    4B7+7301B       RATE ERROR COUNTER
P7302B DATA    4B7+7302B       TERM. NO. FOR P.T. CONTROL LINE
P7303B DATA    4B7+7303B       TERM. NO. FOR P.T. DATA LINE
P7304B DATA    4B7+7304B       LOCAL MEMORY WINDOW FETCH ADDRESS
P7305B DATA    4B7+7305B       FETCH DATA
P7306B DATA    4B7+7306B       STORE ADDRESS
P7307B DATA    4B7+7307B       STORE DATA
PRTBASE DATA   4B7+3300B       PROCESS TABLE BASE ADDRESS
TROUBLE DATA   100000B         ABOUT 30 SECONDS ON THE TIMER.

* INITIAL STATE FOR THE CHIO

ISTATE EQU     *
ISVR4  DATA    162B
IMTAA  DATA    5140B
IMTCA  DATA    5401B
ILDVTA DATA    6250B
IXLINE DATA    117B
IICC   DATA    10572440B
IDVTBA DATA    5764B
IOUTCC DATA    115623B
IMTAE  DATA    5140B
       DATA    0
ILDVTE DATA    6250B
IDVTBW DATA    5764B
ICPUFLG DATA   0
IWTCE2 DATA    2B5
IBFLAG DATA    4B7
ILLINE DATA    12B
ILCWSI DATA    0
ILINE  DATA    0
ICHAR  DATA    0
ILBIT  DATA    2B4
IWD1   DATA    24060102B
IWD2   DATA    213B
IRBUF  DATA    13B
IWBUF  DATA    5B
IRWORD DATA    0
IWWORD DATA    0
IRCPEND DATA   4203B4
IBTIME DATA    6326B
ILBTBA DATA    6360B
ILBTBE DATA    6360B
ISVR2  DATA    745B
ISVR1  DATA    753B
INCI   DATA    31412327B
       DATA    11412510B
       DATA    25432016B
       DATA    05777777B
       DATA    21777777B
       DATA    02000000B
       DATA    0
       DATA    0
IXLINK DATA    0
IFREEL DATA    5770B
IAVB   DATA    77777777B
ICHARI DATA    215B
IPRB   DATA    0
IMP    DATA    2B7
IPLINE DATA    0
IOBITS DATA    77777777B
ICHISC DATA    0
IMAXFB DATA    520B
IMINFB DATA    402B
IUPAM  DATA    144B
ISVR3  DATA    72B
IBUF0  DATA    203B4
ISVR0  DATA    1106B
ISWCNT DATA    1
ITFWK  DATA    0
INSRFLAG DATA  0
IBWORD DATA    0
IRTCE1 DATA    2B6
ILB24T DATA    6330B
ISWORD DATA    61352215B
ISBFLAG DATA   0
ISWCP  DATA    2031762B

* STATE DESCRIPTORS AND TABLES

ACTION DATA    ACTARR,ACTARR+4,ACTARR+8     ACTION
ACTARR BRM     TDTP
       BRM     TDTP
       BRM     TDTP
       BRM     TDTP
       BRM     TDTP
       BRM     TDTP
       BRM     IDTP
       BRM     TDTP
       BRM     TDTP
       BRM     TDTP
       BRM     TDTP
       BRM     TDTP

NEXT   DATA    NEXARR,NEXARR+4,NEXARR+8     NEXT STATE
NEXARR CLA
       BRM     WHICH
       CLA
       CLA
       CLA
       CLA
       LDA     =2
       CLA
       CLA
       CLA
       CLA
       CLA

OUTPUT DATA    OUTARR,OUTARR+4,OUTARR+8     OUTPUT RESPONSE
OUTARR NOP
       BRM     OFFOK
       BRM     NG
       BRM     NG
       BRM     BYE
       BRM     BYE
       NOP
       BRM     BYE
       BRM     BYE
       BRM     BYE
       BRM     BYE
       BRM     BYE

* CHIO TABLES (MUST BE SET UP AT LOAD TIME)

TABLES EQU     25000B

* TEMPORARIES

ADD    BSS     1               MASK FOR CHANGING TERMINAL RATE
CHANGE BSS     1               HOLDS DIRECTION OF LINE STATE CHANGE
CLIN   BSS     1               CONTROL LINE INPUT NUMBER
CLON   BSS     1               CONTROL LINE OUTPUT NUMBER
CLOCW  BSS     1               CONTROL LINE OUTPUT CONTROL WORD
CLOI   BSS     1               CONTROL LINE OUTPUT INDICATOR
CMND   BSS     1               P.T. COMMAND NUMBER
ETC    BSS     1               END TEST CONSTANT
GCOUNT BSS     24              GLITCH FILTER COUNTERS FOR B.S. LINES
GTPLNO BSS     1               TEMPORARY
INLIN  BSS     1               INPUT LOGICAL LINE NUMBER
LAST   BSS     1               PREVIOUS NCIP1 WORD SAMPLE
LINK   BSS     1               LINK FOR BUFFER CONSTRUCTION
LLNO   BSS     1               LOGICAL LINE NUMBER
LSTATE BSS     1               LINE STATE FOR THE B.S. LINES
LT1    BSS     1               TEMPORARY STORAGE
LT2    BSS     1               TEMPORARY STORAGE
MASK   BSS     1               FOR CHECKING OUTPUT REQUIRED
MODE   BSS     1               HOLDS CHIO MODE
NCIP1  BSS     1               USED FOR HORRIBLE KLUDGE
OFFFLG BSS     1               USED TO SYNCHRONIZE OFFOK AND WHICH
OUTLIN BSS     1               OUTPUT LOGICAL LINE NUMBER
PLDVT  BSS     1               LDVT BASE ADDRESS
PLNO   BSS     1               PHYSICAL LINE NUMBER
PMTA   BSS     1               MAIN TABLE BASE ADDRESS
PRTI   BSS     1               PROCESS TABLE INDEX
PRTPTR BSS     1               POINTER TO REFERENCE PRT
POINTR BSS     1               POINTER
PTR1   BSS     1               POINTER
PTR2   BSS     1               POINTER
PTCHR  BSS     1               TEMP. STORAGE
PTDLN  BSS     1               PAPER TAPE DATA LINE NUMBER
PTILCC BSS     1               P.T. INPUT LINE CHAR. COUNT LOCATION
PTRCHR BSS     1               TEMP. STORAGE
PUTT1  BSS     1               TEMPORARY STORAGE
PUTT2  BSS     1               TEMPORARY STORAGE
RDINST BSS     1               USED FOR POSSIBLE ENTRY TO P.T. HANDLER
REMOVE BSS     1               MASK FOR CHANGING TERMINAL RATE
RESOUT BSS     1               TEMPORARY STORAGE
SAMPLE BSS     1               TEMPORARY STORAGE
STATE  BSS     1               P.T. ROUTINE STATE
STRING BSS     1               USED TO COLLECT P.T. COMMANDS
TIMER  BSS     1               USED FOR HORRIBLE KLUDGE
XVAL   BSS     1               INDEX REG. INITIALIZATION

       END