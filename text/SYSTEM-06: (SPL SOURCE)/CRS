     PROGRAM CRS;
       DECLARE INTEGER ARRAY
           UPPER'BOUND[4] ← ( 341,  114,  39,  13),
           LOWER'BOUND[4] ← (-341, -114, -39, -13);
       DECLARE REAL ARRAY
           POSITIVE'FACTOR[4] ← (1E+205, 1E+69, 1E+23, 1E+8),
           NEGATIVE'FACTOR[4] ← (1E-205, 1E-69, 1E-23, 1E-8);
       DECLARE INTEGER ARRAY
           POSITIVE'ADDEND[4] ← ( 205,  69,  23,  8),
           NEGATIVE'ADDEND[4] ← (-205, -69, -23, -8);
       DECLARE INTEGER ARRAY INDEX[30] ←
           (0,0,0,0,1,1,1,2,2,2,3,3,3,3,4,4,4,5,5,5,6,6,6,7,7,7,7,8,8,8);
       DECLARE REAL ARRAY FACTORS[9] ←
           (1E3, 1E2, 1E1, 1E0, 1E-1, 1E-2, 1E-3, 1E-4, 1E-5);
       DECLARE REAL X, Y, Z;
       DECLARE INTEGER I, E, D, EXPONENT;
       DECLARE STRING S;
       DECLARE FIELD
           SIGN(0:0,0),
           BIASED'EXPONENT(0:1,11);
       DECLARE STRING FIELD STRING'FIELD(0);

     STRING ENTRY CRS(REAL X, STRING @CALLER'S'STRING, INTEGER FORMAT), FRETURN;
       S ← CALLER'S'STRING.STRING'FIELD;

       IF X$SIGN DO;
           X ← -X;
           WCI('-', S//FRETURN);
       ELSE DO;
           WCI(' ', S//FRETURN);
       ENDIF;
       WCI('0', S//FRETURN);
       WCI('.', S//FRETURN);

* FIRST, WE CONVERT X TO "STANDARD FORM", USING A METHOD DESCRIBED IN
* THE BCC MEMORANDUM ON "CALLING SEQUENCES AND EXPLANATION OF ARITHMETIC
* ROUTINES AND FLOATING POINT I/O" WRITTEN BY RON PAYNE TO PETER DEUTSCH
* AND RICK DOVE AND DATED JULY 3, 1970.  THE "STANDARD FORM" OF A REAL
* NUMBER, X, IS A REAL NUMBER, Y, AND AN INTEGER, E, SUCH THAT
* .1<=Y<1 AND X = Y*(10**E).

       Y ← X;
       E ← 0;
       IF Y # 0.0 DO;
           FOR I ← 0 TO 3 DO;
               EXPONENT ← Y$BIASED'EXPONENT - 2000B;
               IF EXPONENT >= UPPER'BOUND[I] DO;
                   Y ← Y*NEGATIVE'FACTOR[I];
                   E ← E+POSITIVE'ADDEND[I];
               ELSEIF EXPONENT <= LOWER'BOUND[I] DO;
                   Y ← Y*POSITIVE'FACTOR[I];
                   E ← E+NEGATIVE'ADDEND[I];
               ENDIF;
           ENDFOR;
           EXPONENT ← Y$BIASED'EXPONENT - 2000B;
           Y ← Y*FACTORS[INDEX[EXPONENT+14]];
           E ← E+INDEX[EXPONENT+14]-3;
           IF FIX(Y*10.0) = 0 DO;* I.E., IF Y < 0.1 DO;.
               Y ← Y*10.0;
               E ← E-1;
           ENDIF;
       ENDIF;

* NOW WE HAVE THE "STANDARD FORM" FOR X IN Y AND E.

       FOR I ← 1 TO 11 DO;
           WCI('0' + (D ← FIX(Z ← Y*10.0)), S//FRETURN);
           Y ← Z - FLOAT(D);
       ENDFOR;

       WCI('E', S//FRETURN);
       CNS(E, S//FRETURN);
       CALLER'S'STRING.STRING'FIELD ← S;
       RETURN S;
     END;
